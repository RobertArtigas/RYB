#!-----------------------------------------------------------------------------
#!-----------------------------------------------------------------------------
#!-----------------------------------------------------------------------------
#GROUP(%CopyrightWINPIECES)
  #!BOXED('Version 1.0 [ 2018.03.11 ]'),AT(,,278),PROP(PROP:FontStyle,700)
  #BOXED('Version 1.0 [ 2018.03.11 ]'),AT(,,454),PROP(PROP:FontStyle,700)
    #DISPLAY(''),AT(,,,1)
    #DISPLAY('Add assorted cosmetics to application windows.'   ),AT(10)
    #DISPLAY('Copyright © 2018-2099 by Roberto Artigas y Soler.'),AT(10)
    #DISPLAY('All copyrights reserved world wide.'              ),AT(10)
    #DISPLAY('E-MAIL: roberto.artigas.dev@gmail.com'            ),AT(10)
  #ENDBOXED
#!
#GROUP(%MITLicenseWINPIECES)
#TAB('WINPIECES'),PROP(PROP:FontStyle,700)
  #INSERT(%CopyrightWINPIECES) 
  #INSERT(%MITLicense)
  #INSERT(%ClarionFamily)
#ENDTAB
#!-----------------------------------------------------------------------------
#EXTENSION (RYB_WinPieces_ALL, 'RA.2020.01.16: Assorted cosmetics to application windows'),DESCRIPTION('RYB | Add assorted cosmetics to application windows. Version: ' & %tplVersionWINPIECES & ', Release: ' & %tplReleaseWINPIECES),APPLICATION
#!-----------------------------------------------------------------------------
#INSERT(%PreambleRYB)
#!-----------------------------------------------------------------------------
#! Changes:
#! RA.2020.08.24.MON      Roberto Artigas Jr (request by Wolfgang Orth) 
#!  *   What about letting you enter the picture for DATE/TIME? Done!
#! RA.2020.01.16.THU      Roberto Artigas Jr (request by Dave Schwartz)
#!  *   Added code to CENTER the windows that got moved off the viewable screen (negative coordinates)
#!  *   Added @D05B to the date options (Australia)
#! RA.2019.08.02          Roberto Artigas Jr (assistance by Rick Martin)
#!  *   Added date/time CONTROL changes to the screen to standarize windows
#! RA.2018.12.27.THU      Roberto Artigas Jr
#!  *   Made the width of the templates larger to accomodate C11 wider templates
#! RA.2018.03.11.SAT      Roberto Artigas Jr 
#!  *   Created this template
#PREPARE
#ENDPREPARE
#!-----------------------------------------------------------------------------
#!SHEET,HSCROLL,AT(,,288)
#SHEET,HSCROLL,AT(,,464)
#INSERT(%MITLicenseWINPIECES)
#TAB(UPPER(%Application) & '.' & UPPER(%ProgramExtension))
  #!BOXED(''),AT(,,278)
  #BOXED(''),AT(,,454)
    #PROMPT('Choose the VCR option:',DROP('Turn On|Turn Off|Leave Alone')),%raVCRchoice,DEFAULT('Leave Alone')
  #ENDBOXED
  #! RA.2020.08.21: Per Wolfgang Orth: Does THIS work on a FRAME [???] (Application VS Window)
  #!BOXED(''),AT(,,278)
  #BOXED(''),AT(,,454)
    #DISPLAY('Select Wallpaper Image:')   #! ,PROP(PROP:FontStyle,700)
    #!PROMPT('',OPENDIALOG('Select Wallpaper Image','Images|*.*')),%raWallpaperImage,DEFAULT(''),AT(10,,268)
    #PROMPT('',OPENDIALOG('Select Wallpaper Image','Images|*.*')),%raWallpaperImage,DEFAULT(''),AT(10,,444)
    #PROMPT('Set Wallpaper Type:',DROP('Leave Alone|Tiled|Stretched|Centered|None')),%raWallpaperType,DEFAULT('Leave Alone')
    #ENABLE(%raWallpaperType='None')
      #PROMPT('Enable Background Color',CHECK),%raSetBackground,DEFAULT(%FALSE),AT(10)
      #!ENABLE(%raSetBackground = %TRUE)
        #!PROMPT('Select Background Color',COLORDIALOG('Select Background Color')),%raColorBackground,DEFAULT(COLOR:BTNFACE)
      #!ENDENABLE
    #ENDENABLE
  #ENDBOXED
  #! RA.2020.08.24: Per Wolfgang Orth: What about letting you enter the picture for DATE/TIME? Done!
  #! RA.2020.08.21: Per Wolfgang Orth: What about letting you enter the picture for DATE/TIME [???]
  #!                Reasonable: Could be done. (Germany, Switzerland)
  #! RA.2020.01.16: DATE: Added @D05B options (Dave Schwartz)
  #! RA.2019.08.06: DATE/TIME: DCT DEFAULT|OTHER|WINDOWS SHORT|WINDOWS LONG
  #BOXED(''),AT(,,454)   
    #PROMPT('Enable application wide DATE format?',CHECK)     , %raFormatDateCheck , AT(10) , DEFAULT(0)
    #ENABLE(%raFormatDateCheck) 
      #!PROMPT('Format DATE default: ',DROP('@D010-B|@D02B|@D05B|@D017B|@D018B|@D08B'))  , %raFormatDateMask  , REQ    , DEFAULT('@D010-B')
      #PROMPT('Format DATE default: ',@S120)                  , %raFormatDateMask  , REQ    , DEFAULT('@D010-B')
    #ENDENABLE
    #PROMPT('Enable application wide TIME format?',CHECK)     , %raFormatTimeCheck , AT(10) , DEFAULT(0)
    #ENABLE(%raFormatTimeCheck)
      #!PROMPT('Format TIME default: ',DROP('@T04B|@T06B|@T07B|@T08B'))                  , %raFormatTimeMask  , REQ    , DEFAULT('@T04B')
      #PROMPT('Format TIME default: ',@S120)                  , %raFormatTimeMask  , REQ    , DEFAULT('@T04B')
    #ENDENABLE
  #ENDBOXED
#ENDTAB
#ENDSHEET
#!-----------------------------------------------------------------------------
#!-----------------------------------------------------------------------------
#AT(%CustomGlobalDeclarations)
  #IF(%raWallpaperImage)
    #PROJECT(%raWallpaperImage)
  #ENDIF
#ENDAT
#!-----------------------------------------------------------------------------
#!----------------------------------------------------------------------------- RA.2019.08.02: Added
#AT(%WindowManagerMethodDataSection,'Init','(),BYTE'),PRIORITY(8005)
ThisFeq           LONG(0)  #<! Used for window cosmetic adjustments
WindowXpos        LONG(0)  #<! Check for X position off the screen
WindowYpos        LONG(0)  #<! Check for Y position off the screen
#ENDAT
#!-----------------------------------------------------------------------------
#AT (%WindowManagerMethodCodeSection,'Init','(),BYTE'),PRIORITY(8005)
#EMBED(%RYB_SetWindowMinimumSize,'RYB: Set Window Minimum Size')
! Code to prevent resizer's from getting the screen smaller than the screen design.
! Be aware that the Ctrl Shift F9 windows have a default size that is normally smaller
! than screen design. Where the internal information is not shown. Please take care.
0{PROP:MinWidth}  = 0{PROP:Width}
0{PROP:MinHeight} = 0{PROP:Height}
#ENDAT
#!-----------------------------------------------------------------------------
#! RA.2021.02.14: Noticed EZHelp code WAS generating when this was 'Leave Alone', and NOT generating when 'None'
#! There is a conflict somewhere whit EzHelp. Need to fix the problem.
#!AT (%WindowManagerMethodCodeSection,'Init','(),BYTE'),PRIORITY(8025)          #! RA.2021.03.14: Changed
#AT (%WindowManagerMethodCodeSection,'Init','(),BYTE'),PRIORITY(8010)
  #IF (%raWallpaperType <> 'Leave Alone')
    #IF (%raWallpaperType <> 'None')
!BOG-RYB: Set the Wallpaper properties
0{PROP:WallPaper}   = '~%raWallpaperImage' #<! Set the Wallpaper
      #CASE(%raWallpaperType)
      #OF('Centered')
0{PROP:Centered}    = TRUE
0{PROP:Stretch}     = FALSE
0{prop:Tiled}       = FALSE
      #OF('Stretched')
0{PROP:Centered}    = FALSE
0{PROP:Stretch}     = TRUE
0{prop:Tiled}       = FALSE
      #OF('Tiled')
0{PROP:Centered}    = FALSE
0{PROP:Stretch}     = FALSE
0{prop:Tiled}       = TRUE
      #ENDCASE
!EOG-RYB: Set the Wallpaper properties
#EMBED(%EOGRYB_SetWallpaper,'After: EOG-RYB: Set the Wallpaper properties')
    #ELSE
!BOG-RYB: Clear the Wallpaper properties
0{PROP:WallPaper}   = '' #<! Clear the Wallpaper
      #IF(%raSetBackground=%TRUE)
0{PROP:Background}  = COLOR:BTNFACE #<! Set Background color
#EMBED(%RYB_AjustWallpaper,'RYB: Adjust the Wallpaper properties')
CLEAR(ThisFeq)
LOOP
  ThisFeq = 0{PROP:NextField, ThisFeq}
  IF ThisFeq = 0 THEN BREAK END
  CASE ThisFeq{PROP:Type}
  OF   CREATE:PROMPT
  OROF CREATE:STRING
  OROF CREATE:SSTRING
  OROF CREATE:CHECK
  OROF CREATE:RADIO
     ThisFeq{PROP:TRN}   = TRUE
  END #<! CASE
END #<! LOOP
      #ENDIF
!EOG-RYB: Clear the Wallpaper properties
#EMBED(%EOGRYB_ClearWallpaper,'After: EOG-RYB: Clear the Wallpaper properties')
    #ENDIF
    #!
    #IF((%raFormatDateCheck) OR (%raFormatTimeCheck))
CLEAR(ThisFeq)
LOOP
  ThisFeq = 0{PROP:NextField, ThisFeq}
  IF ThisFeq = 0 THEN BREAK END
  CASE ThisFeq{PROP:Type}
  OF   CREATE:entry
  OROF CREATE:sstring
    #!TD.Debug('BEF: ThisFeq=[ ' & ThisFeq & ' ], =' & TD.DescribeType( ThisFeq{PROP:Type} ) & '=' & TD.GetFEQDescr( ThisFeq ) & '=' & ThisFeq{PROP:Text} & '=.')
    CASE(UPPER(SUB(ThisFeq{PROP:Text},1,1)))
      #IF(%raFormatDateCheck)
    OF 'D'  ; ThisFeq{PROP:Text} = '%raFormatDateMask'
      #ENDIF
      #IF(%raFormatTimeCheck)
    OF 'T'  ; ThisFeq{PROP:Text} = '%raFormatTimeMask'
      #ENDIF
    END!CASE
    CASE(UPPER(SUB(ThisFeq{PROP:Text},1,2)))            #! RA.2019.08.02: Rick Martin: Warns about this posibility.
      #IF(%raFormatDateCheck)
    OF '@D' ; ThisFeq{PROP:Text} = '%raFormatDateMask'
      #ENDIF
      #IF(%raFormatTimeCheck)
    OF '@T' ; ThisFeq{PROP:Text} = '%raFormatTimeMask'
      #ENDIF
   END!CASE
    #!TD.Debug('AFT: ThisFeq=[ ' & ThisFeq & ' ], =' & TD.DescribeType( ThisFeq{PROP:Type} ) & '=' & TD.GetFEQDescr( ThisFeq ) & '=' & ThisFeq{PROP:Text} & '=.')      
  END!CASE
END!LOOP
#!STOP('LOOK: ThisFeq')
    #ENDIF
#EMBED(%EOGRYB_SetDateTimeMask,'After: EOG-RYB: Set the DATE-TIME global properties')
  #ENDIF
#ENDAT
#!-----------------------------------------------------------------------------
#AT (%WindowManagerMethodCodeSection,'Init','(),BYTE'),PRIORITY(8030)
  #IF (%raVCRchoice <> 'Leave Alone')
!BOG-RYB: Set the VCR on the list boxes
#!
CLEAR(ThisFeq)
LOOP
  ThisFeq = 0{PROP:NextField, ThisFeq}
  IF ThisFeq = 0 THEN BREAK END
  IF ThisFeq{PROP:TYPE}  = CREATE:LIST      THEN
     #IF (%raVCRchoice = 'Turn On')
     ThisFeq{PROP:VCR}   = TRUE
     #ELSE
     ThisFeq{PROP:VCR}   = FALSE
     #ENDIF
  END #<! IF
END #<! LOOP
!EOG-RYB: Set the VCR on the list boxes
#EMBED(%EOGRYB_SetListBoxesVCR,'After: EOG-RYB: Set the VCR on the list boxes')
  #ENDIF
#ENDAT
#!-----------------------------------------------------------------------------
#AT(%WindowManagerMethodCodeSection,'Init','(),BYTE'),PRIORITY(8900)
! Code to prevent windows from going off the screen with negative coordinates.
GetPosition(0,WindowXpos,WindowYpos)
IF (WindowXpos < 0 OR WindowYpos < 0)       THEN
  0{PROP:CENTER}        = TRUE
END
#!EMBED(%EOGRYB_CenterTheScreen,'After: EOG-RYB: Center the screen')
#ENDAT
#!-----------------------------------------------------------------------------
#!-----------------------------------------------------------------------------
#!-----------------------------------------------------------------------------
#! https://odata.de/Clarion/colored_background_for_appframe/index.php
#!
#! Quite a while ago Jim Gambon posted a great solution for this on the Clarion Newsgroups.
#!
#! First step:
#! In the After Global Includes: we add an EQUATE()
#! GCL_HBRBACKGROUND            EQUATE(-10) 
#! Then, in the Global Map:
#! ! background color
#!    MODULE('Win32API')
#! GetClassLong        PROCEDURE(LONG thehWnd,LONG theIndex),LONG,PASCAL,NAME('GetClassLongA')
#! SetClassLong        PROCEDURE(LONG thehWnd,LONG theIndex,LONG theNewValue),LONG,PROC,PASCAL,NAME('SetClassLongA')
#! CreateSolidBrush    PROCEDURE(LONG TheColor),LONG,PASCAL,NAME('CreateSolidBrush')
#! DeleteObject        PROCEDURE(LONG theObject),LONG,PROC,PASCAL,NAME('DeleteObject')
#!    END !MODULE
#!
#! ! make sure that "Column 1" is ticked on
#!
#! Add these two variable into your AppFrame window, either in the Data-Embed or the Data-Pad:
#! SaveNewBrush LONG
#! SaveOldBrush LONG
#! The color is obtained after the windows opens, so we put the following into the embed right after its opening in ThisWindow.Init:
#!  SaveOldBrush = GetClassLong(AppFrame{PROP:ClientHandle},GCL_HBRBACKGROUND)
#!  SaveNewBrush = CreateSolidBrush(0FFFFFFh) ! Solid White
#!  IF SaveNewBrush <> 0
#!     SetClassLong(AppFrame{PROP:ClientHandle},GCL_HBRBACKGROUND,SaveNewBrush)
#!  END !IF
#!
#! Its always a good practice to clean up the mess when we leave, so in the TakeCloseEvent handler for ThisWindow object we reset and delete the brush objects:
#!  IF SaveOldBrush <> 0
#!     SetClassLong(AppFrame{PROP:ClientHandle},GCL_HBRBACKGROUND,SaveOldBrush)
#!     SaveOldBrush = 0
#!  END !IF
#!
#!  IF SaveNewBrush <> 0
#!     DeleteObject(SaveNewBrush)
#!     SaveNewBrush = 0
#!  END !IF
#!-----------------------------------------------------------------------------
#!-----------------------------------------------------------------------------
#!-----------------------------------------------------------------------------
